;#####################################################################
;## Macro to help with printing                                     ##
;## Usage: Print [pointer]                                          ##
;#####################################################################
	MAC Print
.ADDRESS SET {1}
	DC $02
	DC #<.ADDRESS
	DC #>.ADDRESS
	ENDM

;#####################################################################
;## Macro to help with printing                                     ##
;## Usage: Print [pointer]                                          ##
;#####################################################################
	MAC PrintIND
.ADDRESS SET {1}
	DC $1B
	DC #<.ADDRESS
	DC #>.ADDRESS
	ENDM

;#####################################################################
;## Macro to show player block                                      ##
;## Usage: Show_player [pointer]                                    ##
;#####################################################################	
	MAC Show_player
.OFFSET SET {1}
.COLOR SET {2}
	LDX .OFFSET
	LDY .OFFSET+1
	DC $04
	DC $0F				; change color to the current player color
	DC #.COLOR
	Print DEF_SNAKEBODY
	ENDM

;#####################################################################
;## Macro to handle keyboard                                        ##
;## Usage: Handle_IO_player [dir pointer] [required acc] [dir]      ##
;## Requires last pressed key in the accumulator                    ##
;#####################################################################	
	MAC Handle_IO_player
.OFFSET SET {1}
.REQKEY SET {2}
.NEWDIR SET {3}
	CMP #.REQKEY			; see if the required key was pressed
	BNE .IO_noaction		; if not, skip further processing
	LDX .OFFSET+1			; load the old direction into the x register
	IF .NEWDIR > 1
	  CPX #.NEWDIR-2		; if the current direction is the opposite
	  BEQ .IO_noaction		; then don't change the direction
	ELSE
	  CPX #.NEWDIR+2		; if the current direction is the opposite
	  BEQ .IO_noaction		; then don't change the direction
	ENDIF
	LDX #.NEWDIR			; load the new direction into the x register
	STX .OFFSET			; and store it at the location of the direction variable
.IO_noaction
	ENDM

;#####################################################################
;## Macro to move player                                            ##
;## Usage: Move_player [pointer]                                    ##
;#####################################################################	
	MAC Move_player
.OFFSET SET {1}
	LDA #$1
	CMP .OFFSET+2			; check if the direction is smaller than 2
	BPL .MV_topleft			; if so, go to MV_topleft
	
	LDA #$2
	CMP .OFFSET+2			; check if the direction is 2
	BEQ .MV_down
	INC .OFFSET			; increase the X
	JMP .MV_end
.MV_down
	INC .OFFSET+1			; increase the Y
	JMP .MV_end
.MV_topleft
	BEQ .MV_left			; if direction is 1, it's left
	DEC .OFFSET+1			; decrease the Y
	JMP .MV_end
.MV_left
	DEC .OFFSET			; decrease the X
.MV_end
	LDA .OFFSET+2			; load the direction var into the accumulator
	STA .OFFSET+3			; and store it in the old direction variable
	ENDM	

;#####################################################################
;## Macro to load field value at given position into accumulator    ##
;## Usage: Field_check [x] [y]                                      ##
;## X = 0..65, Y = 0..21                                            ##
;#####################################################################	
	MAC Field_check
.FX SET {1}
.FY SET {2}
	LDA .FX				; load the x value into the accumulator
	BEQ .F_error			; if X is too small, error
	CMP #$43			; if X is too big, error
	BEQ .F_error			
	LDA .FY				; load the y value into the accumulator
	BEQ .F_error			; if Y is too small, error
	CMP #$17			; if Y is too big, error
	BEQ .F_error
	CLC				; make sure the carry flag is cleared
	ADC #$50			; offset in memory for the field-row
	STA $0001			; place the y value with the offset into the high byte of the indirect address
	LDY .FX				; place the x into the low byte of the indirect address (because of inY)
	DC $B1				; LDA with indirectY addressing mode
	DC $00				; and look at 00:00 and 00:01 for the real address
	JMP .F_end
.F_error
	LDA #$01			; indicate that this field is invalid (1)
.F_end
	ENDM

;#####################################################################
;## Macro to store 1 into field value at given position             ##
;## Usage: Field_load [x] [y]                                       ##
;## X = 1..66, Y = 1..22                                            ##
;#####################################################################	
	MAC Field_set
.FX SET {1}
.FY SET {2}
	LDA .FY				; load the y value into the accumulator
	CLC				; make sure the carry flag is cleared
	ADC #$50			; offset in memory for the field-row
	STA $0001			; place the y value with the offset into the high byte of the indirect address
	LDY .FX				; place the x into the low byte of the indirect address (because of inY)
	LDA #$01			; set the field value to 1
	DC $91				; LDA with indirectY addressing mode
	DC $00				; and look at 00:00 and 00:01 for the real address
	ENDM
	
;#####################################################################
;## Macro to check if the game is at an end                         ##
;## Usage: Check_collision                                          ##
;## Load the accumulator with:                                      ##
;## 0: game continues                                               ##
;## 1: player 1 wins                                                ##
;## 2: player 2 wins                                                ##
;## 3: tie (head-on collision)                                      ##
;## 4: tie (both died)                                              ##
;#####################################################################
	MAC Check_collision
	LDA PLAYER1_X			; load the player 1 X into the accumulator
	CMP PLAYER2_X			; and compare it to player 2's X
	BNE .C_notie			; if not equal, it's definitely not a tie
	LDA PLAYER1_Y			; load the player 1 Y into the accumulator
	CMP PLAYER2_Y			; and compare it to player 2's Y
	BNE .C_notie			; if not equal, it's definitely not a tie
	LDA #$03			; otherwise, it's a tie
	JMP .C_end			; and stop processing
.C_notie
	Field_check PLAYER1_X, PLAYER1_Y	; check if player 1 is at an illegal position
	BEQ .C_check2			; if player 1 is at a valid position, check for player 2
	Field_check PLAYER2_X, PLAYER2_Y	; check if player 2 is at an illegal position
	BNE .C_tie			; if player 2 is at an invalid position too, jump to tie #4.
	LDA #$02			; otherwise, only player 1 is at an invalid position, so player 2 wins
	JMP .C_end
.C_check2
	Field_check PLAYER2_X, PLAYER2_Y	; check if player 2 is at an illegal position
	BEQ .C_end			; if player 2 is at a valid position too, jump to end. Accumulator is still 0
	LDA #$01			; otherwise, player 1 wins
	JMP .C_end
.C_tie
	LDA #$04			; both player made are at an illegal position, but not the same
.C_end
	ENDM

	processor 6502
	org $8000
	
	jmp main			; start with the program

	INCLUDE "vars.c64"		; load string definition
	INCLUDE "ai.c64"		; load AI 1.0
	INCLUDE "ai3.c64"		; load AI 3.0
	INCLUDE "menu.c64"		; load main menu
	
;#####################################################################
;## main: the main program                                          ##
;#####################################################################
main
	JSR main_menu
	JSR randomize_game
	JSR clear_field
	JSR init_screen
	
game_loop
	DC $0B				; sleep 5ms (one tick)
	DC $05
	DC $03				; get key into accumulator

	CMP #$00			; see if any key has been pressed
	BEQ game_step			; if not, continue with the loop
	
	CMP #$51			; see if key was 'Q'
	BNE game_no_end1		; if not, don't quit
	JMP game_exit_now
game_no_end1
	
	CMP #$70			; check if the key was 'p'
	BNE nopause			; if not, don't pause the game
	LDA #$70			; check for the key p
	STA $0002			; and store this key in 00:02
	JSR pauser			; pause until p has been pressed again
nopause
	
	JSR handle_keys			; update directions based on keys
	
game_step
	INC FRAMETICK			; increase the frame ticker with one
	LDA FRAMETICK			; load the frame ticker into the accumulator
	CMP #$10			; check if more than 16 (0x10) ticks have passed since last game step
	BNE game_loop			; if not, continue with the loop
	
	LDA AI_LEVEL			; load the AI level into the accumulator
	BEQ skip_AI			; if the AI level is 0, there is no AI
	
	JMP (AI_LEVEL)			; otherwise, perform the snake AI_1
	
skip_AI
	
	Move_player PLAYER1		; move the players
	Move_player PLAYER2
	
	Check_collision			; see if the game is at an end
	BEQ game_no_end2		; if not, don't stop
	JMP game_end			; otherwise, stop
game_no_end2	
	
	Field_set PLAYER1_X, PLAYER1_Y	; flag the new position of player 1
	Field_set PLAYER2_X, PLAYER2_Y	; flag the new position of player 2
	
	Show_player PLAYER1, #$31	; print new player positions
	Show_player PLAYER2, #$34
	
	
	
	LDA #$00			; reset the frame ticker to 0
	STA FRAMETICK
	
	JMP game_loop
	
game_end
	
	LDX #$46			; move to (70;4)
	LDY #$04
	DC $04
	
	DC $0F				; set color to green text
	DC $32
	
	CMP #$03			; see if it was a tie
	BMI game_end_win		; if not, display winner
	
	LDX #$48			; move to (72;4)
	DC $04
	Print DEF_TIE
	
	JMP game_end_wait		; now wait for the Q key to be pressed

game_end_win
	Print DEF_PLAYER		; print Player
	CLC				; clear carry flag for the addition
	ADC #$30			; convert the number to an ascii digit
	STA $7000			; store it at 70:00
	LDA #$00			; and store a null
	STA $7001			; at 70:01
	Print $7000			; then print this digit
	
	LDX #$48			; move to (72;5)
	LDY #$05
	DC $04
	Print DEF_WINS
game_end_wait
	
	LDA #$51			; check for the Q key, by storing it in the accumulator
	STA $0002			; and then in 00:02
	JSR pauser			; now pause until this key has been pressed
	
game_exit_now
	
	DC $db

;#####################################################################
;## handle_keys:                                                    ##
;## updates player directions based on pressed key                  ##
;## Player 1 keys: w,a,s,d, ie 0x77, 0x61, 0x73, 0x64               ##
;## Player 2 keys: arrows, ie 0x1B + 0x5B + [0x41,0x44,0x42,0x43]   ##
;#####################################################################
handle_keys
	Handle_IO_player PLAYER1_DIR, #$77, #$00		; if w is pressed, move player 1 up
	Handle_IO_player PLAYER1_DIR, #$61, #$01		; if a is pressed, move player 1 left
	Handle_IO_player PLAYER1_DIR, #$73, #$02		; if s is pressed, move player 1 down
	Handle_IO_player PLAYER1_DIR, #$64, #$03		; if d is pressed, move player 1 right
	
	LDX IS_ARROW			; load the arrow start sign flag into x
	CPX #$2				; see if the arrow flag is ready (2)
	BNE handle_keys_no_arrows	; if no arrow key has been pressed, don't process for player 2
	Handle_IO_player PLAYER2_DIR, #$41, #$00		; if A is pressed, move player 2 up
	Handle_IO_player PLAYER2_DIR, #$44, #$01		; if D is pressed, move player 2 left
	Handle_IO_player PLAYER2_DIR, #$42, #$02		; if B is pressed, move player 2 down
	Handle_IO_player PLAYER2_DIR, #$43, #$03		; if C is pressed, move player 2 right
handle_keys_no_arrows
	
	LDX #$00			; new value for IS_ARROW flag is always 0
	CMP #$1B			; see if the arrow start sign has been set
	BNE handle_key_not_arrowst	; if it's not the arrow start sign, skip setting the flag
	LDX #$01			; if it is, set the flag to 1
	JMP handle_key_not_followup	; if the current key is the arrow start sign, it's not the followup sign
handle_key_not_arrowst
	LDY IS_ARROW			; see if the IS_ARROW flag is currently on
	CPY #$01			; see if the flag is currently 1
	BNE handle_key_not_followup	; if not, this can't be the followup flag, so skip setting the flag
	CMP #$5B			; see if the followup flag has been set
	BNE handle_key_not_followup	; if the followup ([) is not set, skip setting the flag
	LDX #$02			; otherwise, if the previous sign was the arrow start sign and this is the 
handle_key_not_followup
	STX IS_ARROW			; and store the flag
	
	RTS

;#####################################################################
;## randomize_game: randomizes the player positions                 ##
;#####################################################################
randomize_game:
	DC $07				; get a random number that's not bigger than 21
	DC $15				; 
		LDA #$10
	STA PLAYER1_Y			; store it as player1 y
	INC PLAYER1_Y			; and increment it with 1 to start at y=1 at the highest
	DC $07				; get a random number that's not bigger than 21
	DC $15				; 
		LDA #$10
	STA PLAYER2_Y			; store it as player2 y
	INC PLAYER2_Y			; and increment it with 1 to start at y=1 at the highest
	RTS

;#####################################################################
;## clear_field:                                                    ##
;## clears game field                                               ##
;## 51:01 to 66:42, 0 = empty, 1 = not empty                        ##
;#####################################################################
clear_field:
	LDY #$01			; the counter for X in the Y register
	LDX #$51			; the counter for Y in the X register
	LDA #$00			
	STA $0000			; store the accumulator as the lower byte of the relative address
	STX $0001			; load the indirect address to be [field y]:00
	LDA #$00			; default value for field is 00
	
fill_loop
	DC $91				; STA with indirectY addressing mode
	DC $00				; and look at 00:00 and 00:01
	
	
	INY				; increment the X counter with one
	CPY #$43			; see if the x counter is too big
	BNE fill_loop			; if not, continue with the horizontal loop
	LDX $0001			; get the y counter into the X register
	CPX #$67			; if it's too big
	BEQ clear_end			; then, stop with the loop
	INC $0001			; otherwise, increment the Y counter with one
	LDY #$01			; and reset the X counter
	JMP fill_loop			; then repeat the horizontal loop for the next line
clear_end
	
	RTS

;#####################################################################
;## init_screen: initialises the game screen with a #-border        ##
;#####################################################################
init_screen
	DC $0F				; setcolor, red text with dark yellow bg
	DC $31		
	DC $0C				; clear screen
	LDX #$0
	LDY #$0
	DC $04				; move the cursor to (0,0)
	
init_screen_xloop			; this loop prints horizontal lines
	PRINT DEF_BORDER		; print the border
	INX				; increment x with one
	CPX #$50			; print this 80 times
	BNE init_screen_xloop
	
	LDX #$00			; put the x back to 0
	
	CPY #$00			; see if the y is 0
	BNE init_screen_x_end		; if y was not 0, skip with horizontal lines
	
	LDY #$17			; set y to 23 (0x17)
	DC $04				; move the cursor to the new position (0;17)
	JMP init_screen_xloop		; draw a new horizontal line
	
init_screen_x_end	
	
	LDY #$01			; start the y position at 1
	DC $04				; move the cursor to this new position (0;1)
	
init_screen_yloop			; this loop prints vertical lines
	DC $04				; move the cursor
	Print DEF_BORDER		; print DEF_BORDER
	INY				; increment the y with one
	CPY #$17			; see if this is the last y (23, 0x17)
	BNE init_screen_yloop
	
	LDY #$01			; load the y with 1 again
	
	CPX #$00			; see if the x is 0
	BNE init_screen_y_end		; if it's not, we're done with vertical outer lines
	
	LDX #$4F			; load x with 79 (0x4F)
	
	JMP init_screen_yloop		; and print the right vertical line
	
init_screen_y_end
	
	CPX #$4F			; see if x is 79 (0x4F), for the right outer line
	BNE init_screen_end		; if not, we're done with all lines
	
	LDX #$43			; load x with 67 (0x43), for middle-right vertical line
	JMP init_screen_yloop
	
init_screen_end
	
	LDX #$47			; move to (71;1)
	LDY #$1
	DC $04
	Print DEF_SUPER			; and print 'Super'
	INY				; go to the next line
	DC $04
	Print DEF_SNAKE			; and print 'Snake'
	
	LDX #$46			; move to (70;21)
	LDY #$15
	DC $04
	Print DEF_QUIT			; and print the quit string
	
	LDY #$14			; move to (70;20)
	DC $04				
	Print DEF_PAUSE
	
	Show_player PLAYER1, #$31	; print new player positions
	Show_player PLAYER2, #$34
	
	Field_set PLAYER1_X, PLAYER1_Y	; flag the new position of player 1
	Field_set PLAYER2_X, PLAYER2_Y	; flag the new position of player 2
	
	RTS

;#####################################################################
;## pauser: pauses the program until any key has been pressed       ##
;#####################################################################
pauser
	DC $0B				; sleep for 1ms
	DC $01
	
	DC $03				; getkey
	
	CMP $0002			; compare the accumulator (last pressed key) to the value in 00:02
	BNE pauser			; if that key has not been pressed, go back to loop
	RTS

